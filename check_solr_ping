#!/usr/bin/perl
use strict;
use LWP::UserAgent;
use XML::XPath;

#
# Check Solr status using the Ping handler 
# e.g. check_solr_ping http://localhost:8983/solr/admin/ping
#
# (c) 2011 Robin Bramley, Ixxus Ltd.
# Copying and distribution of this file, with or without modification,
# are permitted in any medium without royalty provided the copyright
# notice and this notice are preserved.  This file is offered as-is,
# without any warranty.
#

# Nagios return values
# OK = 0
# WARNING = 1
# CRITICAL = 2
# UNKNOWN = 3

my $retStr = "Unknown - plugin error";
my @alertStrs = ("OK", "WARNING", "CRITICAL", "UNKNOWN");
my $exitCode = 3;
my $numArgs = $#ARGV + 1;

# check arguments
if ( $numArgs != 1 && $numArgs != 3 ) {
  print "Usage: check_solr_ping url [username password]\n";
  exit $exitCode;
}

my $ua = LWP::UserAgent->new;
my $req = HTTP::Request->new( GET => $ARGV[0] );

# perform basic auth if necessary
if ( $numArgs == 3 ) {
  $req->authorization_basic( $ARGV[1], $ARGV[2] );
}

# make request to Solr
my $res = $ua->request($req);

# if we have a HTTP 200 OK response
if ( $res->is_success ) {
  # get content as XML
  my $xp = XML::XPath->new( xml => $res->content );

  $retStr = "Solr ping status";
  
  if ( $xp->exists("/response[str='OK']/str[\@name='status']") ) {
    # We've got <str name="status">OK</str>
    $exitCode = 0;
  }
  else {
    $exitCode = 2;
  }
}
else {
  $retStr = $res->status_line;
  $exitCode = 1;
}

print $alertStrs[$exitCode] . " - $retStr\n";
exit $exitCode;
